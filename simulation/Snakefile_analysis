rule all:
    input:
        config['out']['outdir']+"/r_vis/sim_Recall_allcovs.pdf",
        config['out']['outdir']+"/r_vis/sim_Precision_allcovs.pdf",
        expand(config['out']['outdir']+"/r_vis/simdensity_{cov}x_{te_family}.pdf", cov=config['submit']['cov_range'], te_family=config['config']['family']),
        expand(config['out']['outdir']+"/r_vis/simtsd_{cov}x_{te_family}.pdf", cov=config['submit']['cov_range'], te_family=config['config']['family'])

rule summary:
    input:
        config['out']['outdir']+"/{cov}/"
    conda:
        config['resources']['condaenv']
    output:
        config['out']['outdir']+"/{cov}/"+"/summary/combined_metrics.csv"
    params:
        out=config['out']['outdir']+"/{cov}/",
        exclude=config['analysisparams']['exclude'],
        script=config['analysis']['summary']
    log:
        config['out']['outdir']+"/logs/summary_{cov}.log"
    shell:
        "python {params.script} -o {params.out} &> {log}"

rule r_vis_precision_recall:
    input:
        expand(config['out']['outdir']+"/{cov}/"+"/summary/combined_metrics.csv", cov=config['submit']['cov_range'])
    conda:
        config['resources']['renv']
    output:
        config['out']['outdir']+"/r_vis/sim_Recall_allcovs.pdf",
        config['out']['outdir']+"/r_vis/sim_Precision_allcovs.pdf"
    params:
        mccdir=config['mccdir'],
        out=config['out']['outdir'],
        script=config['analysis']['r_vis_precision_recall']
    log:
        config['out']['outdir']+"/logs/r_vis_precision_recall.log"
    shell:
        "Rscript --vanilla {params.script} {params.mccdir} {params.out} {input} &> {log}"

rule r_vis_for_cov:
    input:
        config['out']['outdir']+"/{cov}/"
    conda:
        config['resources']['renv']
    output:
        expand(config['out']['outdir']+"/r_vis/simdensity_{{cov}}x_{te_family}.pdf", te_family=config['config']['family']),
        expand(config['out']['outdir']+"/r_vis/simtsd_{{cov}}x_{te_family}.pdf", te_family=config['config']['family'])
    params:
        mccdir=config['mccdir'],
        out=config['out']['outdir'],
        script=config['analysis']['r_vis_for_cov'],
        family=config['config']['family']
    log:
        config['out']['outdir']+"/logs/r_vis_for_cov{cov}.log"
    shell:
        "Rscript --vanilla {params.script} {params.mccdir} {params.out} {wildcards.cov} {params.family} &> {log}"

